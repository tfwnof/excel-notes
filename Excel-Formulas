Rank without duplicates:
=RANK(A1,$A$1:$A$10,0)+COUNTIF($A$1:A10,A1)-1

Select range with unknown number of rows:
$A$1:INDEX($A:$A,COUNTA($A:$A))   

return each element in a list once:
{=IFERROR(INDEX(Table1[Column1], MATCH(0,COUNTIF($A$1:A1, Table1[Column1]), 0)),"")}

average growth (arithmetic), directly from data:
=(SUMPRODUCT(B1:E1,1/A1:D1)/COUNT(A1:D1))-1
where:
  A1:E1 is the range containing all elements

Product of returns for a specific date range:
{=PRODUCT(IF($A$1:$A$100>=C3, IF($A$1:$A$100<=D3,$B$3:$B$100)))}
where :
  A:A -> dates
  B:B -> returns
  C:C -> Beginning of period
  D:D -> End of period

Conditional sum, where the sum range is variable:
=SUMIF(rows_range, criteria, index(var_col,0,var_cols_range))
=SUMIF('2022'!$AD$2:INDEX('2022'!$AD:$AD,COUNTA('2022'!$AD:$AD)),Summary!B4,INDEX('2022'!$D$2:INDEX('2022'!$AC:$AC,COUNTA('2022'!$AC:$AC)),0,MATCH(Summary!$B$2,'2022'!$D$1:$AC$1,0)))


Returns an element with the nth largest value with variable columns:
=INDEX(
  elements,
  MATCH(
    LARGE(
      INDEX(
        allvalues,
        0,
        MATCH(
          ref-colname,
          colsrow,
          0)),
      nth_largest),
    INDEX(
      allvalues,
      0,
      MATCH(ref-colname,colsrow,0)),
  0))
=INDEX(elements,MATCH(LARGE(INDEX(allvalues,0,MATCH(ref-colname,colsrow,0)),nth_largest),INDEX(allvalues,0,MATCH(ref-colname,colsrow,0)),0))
=INDEX(Analysis!$B$116:$B$157,MATCH(LARGE(INDEX(Analysis!$C$116:$N$157,0,MATCH(Summary!$B$26,Analysis!$C$115:$N$115,0)),A28),INDEX(Analysis!$C$116:$N$157,0,MATCH(Summary!$B$26,Analysis!$C$115:$N$115,0)),0))
